ext.configurePublishing = { packageName, packageDesc, packageVersion ->
    tasks.register('sourcesJar', Jar) {
        it.dependsOn classes
        it.archiveClassifier.set('sources')
        it.from sourceSets.main.allSource
    }
    tasks.register('javadocJar', Jar) {
        it.dependsOn javadoc
        it.archiveClassifier.set('javadoc')
        it.from javadoc.destinationDir
    }
    publishing {
        repositories {
            maven {
                name = 'Rajce'
                def releasesRepoUrl = 'https://repo.rajce.dev/releases'
                def snapshotsRepoUrl = 'https://repo.rajce.dev/snapshots'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = System.getenv('REPOSILITE_USERNAME')
                    password = System.getenv('REPOSILITE_PASSWORD')
                }
            }
        }
        publications {
            imgui(MavenPublication) {
                groupId = 'io.github.spair'
                artifactId = packageName
                version = packageVersion

                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = packageName
                    description = packageDesc
                    url = 'https://github.com/SpaiR/imgui-java'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'SpaiR'
                            name = 'Ilya Prymshyts'
                            email = 'klimbetoo@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/SpaiR/imgui-java.git'
                        developerConnection = 'scm:git:https://github.com/SpaiR/imgui-java.git'
                        url = 'https://github.com/SpaiR/imgui-java.git'
                    }
                }
            }
        }
    }
}
